# 实验项目3

## 个人信息(github ver)

- 专业：18级计算机科学二班 
- 学号：18340057
- 这一波507头都要写炸了

## 实验题目

C与汇编开发独立批处理的内核

## 实验目的

1. 加深理解操作系统内核概念

2. 了解操作系统开发方法

3. 掌握汇编语言与高级语言混合编程的方法

4. 掌握独立内核的设计与加载方法

5. 加强磁盘空间管理工作

## 实验要求

1. 知道独立内核设计的需求

2. 掌握一种x86汇编语言与一种C高级语言混合编程的规定和要求

3. 设计一个程序，以汇编程序为主入口模块，调用一个C语言编写的函数处理汇编模块定义的数据，然后再由汇编模块完成屏幕输出数据，将程序生成COM格式程序，在DOS或虚拟环境运行。

4. 汇编语言与高级语言混合编程的方法，重写和扩展实验二的的监控程序，从引导程序分离独立，生成一个COM格式程序的独立内核。

5. 再设计新的引导程序，实现独立内核的加载引导，确保内核功能不比实验二的监控程序弱，展示原有功能或加强功能可以工作。

6. 编写实验报告，描述实验工作的过程和必要的细节，如截屏或录屏，以证实实验工作的真实性

## 实验内容

1.  寻找或认识一套匹配的汇编与c编译器组合。利用c编译器，将一个样板C程序进行编译，获得符号列表文档，分析全局变量、局部变量、变量初始化、函数调用、参数传递情况，确定一种匹配的汇编语言工具，在实验报告中描述这些工作。

2. 写一个汇编程和c程序混合编程实例，展示你所用的这套组合环境的使用。汇编模块中定义一个字符串，调用C语言的函数，统计其中某个字符出现的次数（函数返回），汇编模块显示统计结果。执行程序可以在DOS中运行。

3. 重写实验二程序，实验二的的监控程序从引导程序分离独立，生成一个COM格式程序的独立内核，在1.44MB软盘映像中，保存到特定的几个扇区。利用汇编程和c程序混合编程监控程序命令保留原有程序功能，如可以按操作选择，执行一个或几个用户程序、加载用户程序和返回监控程序；执行完一个用户程序后，可以执行下一个。

4. 利用汇编程和c程序混合编程的优势，多用c语言扩展监控程序命令处理能力。

5. 重写引导程序，加载COM格式程序的独立内核。

6. 拓展自己的软件项目管理目录，管理实验项目相关文档

## 实验过程

1. 首先我们对一个样板c程序进行编译，查看编译结果并分析。

   样板c程序如下：

   ```c
   /* 样板c程序example.c */
   int a=3,b=4,c;
   void f(int,int );
   void cmain(){
   	f(a,b);
   } 
   void f(int u,int v){
     c=u+v;
   }
   ```

   ​		在这个程序中，定义了全局变量`a`,`b`,`c`并对前两个进行了初始化。同时有两个程序`cmain`和`f`，`cmain`调用`f`，`f`将c赋值为传入参数u和v的和。

   ​		在当前文件夹下使用指令`gcc -S example.c`即可生成汇编源代码文件`example.s`

   ​		`upper.s`的具体内容与分析如下：

   ```assembly
   	.file	"example.c"
   	.globl	_a
   	.data
   	.align 4
   _a:
   	.long	3
   	.globl	_b
   	.align 4
   _b:
   	.long	4
   	.comm	_c, 4, 2
   ```

   ​		在数据段内，汇编代码首先定义了名为`_a` `_b` `_c`的全局变量，与原c程序的`a` `b` `c`对应。.其中`a`,`b`通过`.align`语句被分配了4字节的空间，通过`.long`语句被赋值。可以看出：汇编语言中的变量是在c中变量前加上下划线得到的。

   ```assembly
   	.text
   	.globl	_cmain
   	.def	_cmain;	.scl	2;	.type	32;	.endef
   _cmain:
   	pushl	%ebp
   	movl	%esp, %ebp
   	subl	$24, %esp
   	movl	_b, %edx
   	movl	_a, %eax
   	movl	%edx, 4(%esp)
   	movl	%eax, (%esp)
   	call	_f
   	nop
   	leave
   	ret
   ```

   ​		接着，在代码段定义了函数`_cmain`，与c源码中的`cmain`对应。可见，c函数前加上下划线可以得到汇编代码的函数。在调用函数`_f`前，先用`pushl`语句将`%ebp`入栈，即存储了当前的栈顶位置。再将新的栈顶值`%esp`赋给`%ebp`。变量`_a` `_b`分别赋值给`%edx` `%eax`，并入作为传递的参数入栈。之后调用函数`_f`。

   ```assembly
   	.globl	_f
   	.def	_f;	.scl	2;	.type	32;	.endef
   _f:
   	pushl	%ebp
   	movl	%esp, %ebp
   	movl	8(%ebp), %edx
   	movl	12(%ebp), %eax
   	addl	%edx, %eax
   	movl	%eax, _c
   	nop
   	popl	%ebp
   	ret
   	.ident	"GCC: (tdm-1) 5.1.0"
   ```

   ​		在函数`_f`内，通过以`%ebp`为基址，取偏移量为8和12的地址，从栈中取出`_a`和`_b`并赋给`%edx`和`%eax`，加和后赋给`_c`。

   从以上的分析中可以看出：

   - 在汇编源代码中，变量名为c语言变量名前加上下划线`_`

   - 参数传递时，字符串首址和整数都是4个字节压栈，返回地址是8个字节，调用指令之后修正`%ESP`指针，消除栈中参数。
   - 调用C函数时，参数按后面参数先进栈的顺序压栈。

   - 全局变量和函数需要`.globl`声明，而函数内的局部变量在汇编代码中不一定有变量名

   - 函数间的调用与返回通过`call`和`ret`实现，内含了`%ebp`的入栈和出栈

     

2. 下面写一个汇编程和c程序混合编程实例。统计在汇编代码中定义一个字符串，在c语言中统计某个字符出现的次数并返回由汇编打印结果。我使用的c编译器为`gcc`，x86汇编器为`nasm`，链接器为`ld`。

   c语言的模块如下：

   ```c
   /*	countStr.c	*/
   
   char ch = 'a';
   extern char str[];
   char countChar(){
   	char tmp = ch;
   	char ans = '0';
   	for(int i=0; i<21; i++){
           if(tmp==str[i])ans++;
       }
   	return ans;
   }
   ```

   `ch`为需要统计个数的字符，会在汇编语言中从键盘读入。`str`为会在汇编语言中定义的字符串，长度为21字节。`countChar`函数统计的`str`中和`ch`相同字符的个数并返回结果`ans`。个数的数字也由字符表示。

   x86汇编语言的模块如下：

   ```assembly
   ;initStr.asm
   
   BITS 16
   global _str
   global _start
   extern _countChar
   extern _ch
   	
   _start:
       mov ax,cs
       mov ds,ax
       mov es,ax
       mov ss,ax
   	
   	mov	bp, _str		 ; BP=当前串的偏移地址
   	mov	ax, ds			 ; ES:BP = 串地址
   	mov	es, ax			 ; 置ES=DS
   	mov	cx, 21	 		 ; CX = 串长
   	mov	ax, 1301h		 ; AH = 13h（功能号）、AL = 01h（光标置于串尾）
   	mov	bx, 0007h		 ; 页号为0(BH = 0) 黑底白字(BL = 07h)
      	mov dh, 0		     ; 行号=0
   	mov	dl, 0			 ; 列号=0
   	int	10h			     ; BIOS的10h功能：显示一行字符
   ```

   ​	首先利用BIOS调用的`int 10H`，即打印字符串的功能，将需要进行统计的字符串`_str`输出到屏幕上。
   ```assembly
   go:	
   	;读取要计数的字符
   	mov ah, 0				 
   	int 16H				;调用无回显的键盘读取，读入字符存入al
   	mov byte[_ch],al
       call _countChar
   ```
   ​	接着调用BIOS的`int 16H`功能，设置`ah`寄存器为0，即从键盘上读取一个字符，存入`al`。接着使用`mov byte[_ch],al`将结果存入字符变量`_ch`。然后调用函数`_countChar`。

   ```assembly
   mov dx,ax		;返回值暂存在dx中
   mov	ax,0B800h	; 文本窗口显存起始地址
   mov	gs,ax		; GS = B800h
   mov bp,0100h
   mov ah,0Fh
   mov al,dl		;把返回值返回al（返回值只有一个字节）
   mov word[gs:bp],ax
   inc bp
   inc bp			;显存调到下一位置
   mov al,byte[_ch]
   mov word[gs:bp],ax
   _end:
       jmp go
   _str:
       db 'abbcccddddeeeeeffffff'
   ```

   ​	`_countChar`的返回值将存入寄存器`ax`中，第一句`mov dx,ax`将返回值暂存到`dx`中，然后设置参数，在显存`B8100h`处打印结果，即字符数。之后在`B8102h`处打印统计的字符。`_end`中进行`jmp go`回到键盘读入之前，允许用户输入其他字符，重新调用`_countChar`，实现对其他字符的统计。最后是对`_str`字符串的定义，为`"abbcccddddeeeeeffffff"`。

   ​	使用`gcc -march=i386 -m32 -mpreferred-stack-boundary=2 -ffreestanding -c countStr.c -o countStr.o `对c进行编译生成文件`countStr.o`；

   ​	使用`nasm -f elf32 initStr.asm -o initStr.o`对x86汇编文件进行汇编生成`initStr.o`;

   ​	使用`ld -m elf_i386 -N initStr.o countStr.o -Ttext 0x7c00 --oformat binary -o strCount.img`对`initStr.o`和`countStr.o`进行链接，其中`-Ttext 0x7c00`将程序初始化在内存的`0x7c00`位置，生成的文件`strCount.img`可以直接作为映象盘的首扇区运行。

   ​	运行结果如下图所示：
   ​	开机后，首先显示的内容如下：

   ![1](PIC\\1.png)

   ​	可以看到，字符`str`(即汇编中的`_str`)被打印在了屏幕上。接着可以输入一个字符进行统计。比如我输入字符`c`，结果如下所示：

   ![2](PIC\\2.png)

   ​		字符串`str`中有3个字符`c`，可以看到统计的个数和统计的字符被打印在了屏幕上。此时还可以继续输入其他字符进行统计。比如我再输入`f`，以下为统计结果：

   ![3](PIC\\3.png)

   ​		同样也可以统计不存在的字符，比如输入`q`:

   ​			![4](PIC\\4.png)

   

   3. 之后是将首扇区引导程序和操作系统内核分开，让首扇区将内核读入内存，接管控制权后让操作系统实现对用户程序的调用。

      首扇区的引导程序的代码如下所示：

      ```asm
      ;MBR.asm
      org 7c00h
      MonitorSWPos equ 8100h		;将监控程序载入此地址
      start:
      	mov ax,cs
      	mov es,ax		; ES = 0
      	mov ds,ax		; DS = CS
      	mov es,ax		; ES = CS
      	;打印提示信息
      	mov	bp, Message		 		; BP=当前串的偏移地址
      	mov	ax, ds					; ES:BP = 串地址
      	mov	es, ax		 			; 置ES=DS
      	mov	cx, MessageLength  		; CX = 串长
      	mov	ax, 1301h		 		; AH = 13h（功能号）、AL = 01h（光标置于串尾）
      	mov	bx, 0007h		 		; 页号为0(BH = 0) 黑底白字(BL = 07h)
         	mov dh, 0		       		; 行号=0
      	mov	dl, 0			 		; 列号=0
      	int	10h			 			; 打印提示信息
      	;读入监控程序
      	mov ax,cs                ;段地址,存放数据的内存基地址
      	mov es,ax                ;设置段地址（不能直接mov es,段地址）
      	mov bx, MonitorSWPos	 ;偏移地址, 存放数据的内存偏移地址
      	mov ah,2                 ;功能号2,表示读入
      	mov al,1                 ;扇区数
      	mov dl,0                 ;驱动器号 ; 软盘为0，硬盘和U盘为80H
      	mov dh,0                 ;磁头号 ; 起始编号为0
      	mov ch,0                 ;柱面号 ; 起始编号为0
      	mov cl,2				 ;1号扇区，即监控程序位置
      	int 13H 				 ;调用读磁盘BIOS的13h功能,将监控程序读入
      	
      	jmp 0x800:0x100
      	
      afterRun:
      	jmp $
      
      Message:
          db '18340057OS is running.Open Monitor Software...'
      MessageLength  equ ($-Message)
      ```

      在首扇区主要实现了以下功能：

      - 利用BIOS调用`int 10h`打印开始运行的提示信息
      - 利用BIOS调用`int 13h`将软盘的第二个扇区的代码读入内存的8100h处（第二个扇区存储的是监控程序，之后第三到六个扇区分别是四个不同的用户程序）
      - 利用`jmp 0x800:0x100`移交控制权给监控程序。使用该语法后会有`cs=800h,ip=100h`，在监控程序中只需要偏移`100h`的数据量即可。

   4. 监控程序的混编实现

      本次的监控程序，我采用了c语言与x86汇编语言混编的方式编写。

      x86汇编语言部分如下：

      ```assembly
      ;MonitorSoftware1.asm
      BITS 16
      UserProPos equ 1000h		;将用户程序载入此地址
      extern _choosePro
      global _start
      extern  _input
      
      _start:
      	mov ax,cs
      	mov es,ax		; ES = 800h
      	mov ds,ax		; DS = CS
      	mov es,ax		; ES = CS
      	jmp print
      ```

      ​		在程序开头调整好各个段地址后进入`print`模块打印提示信息。此处的`cs`为8000h。注意，在开头处声明的`UserProPos equ 1000h`采用的是相对地址，也就是说，之后读入用户程序进入内存，所在的内存地址为8000h+1000h=9000h。

      ​		此外，本段还用`extern`声明了`_choosePro`函数和`_input`变量，便于之后调用c语言的相关功能。

      ```assembly
      ;清屏
      clearScreen:	
      	mov	ax,0B800h	; 文本窗口显存起始地址
      	mov	gs,ax		; GS = B800h
      	mov ax,0
      	mov bp,ax		; BP = 0
      cls:
      	mov byte[gs:bp], ' '
      	inc bp
      	mov byte[gs:bp], 0fh
      	inc bp
      	mov ax, bp
      	xor ax, 7000h
      	jnz cls
      ```

      ​		之后是清屏模块，如上方代码块所示。在实验二中我也实现了清屏功能，在本次实验中，我将清屏模块单独打包，可以在之后输入指令调用，方便用户体验和丰富指令内容。

      ​		清屏模块的主要方法是使得`gs=0b800h`，`bp`从0h递增到7000h，实现对所有显存的遍历。所有的显存的字符改为空格，颜色改为黑底亮白字（因为字符是空格，亮白字实际上不会显示），即0fh，以达到清屏的功能。

      ```assembly
      print:
      	;打印提示信息
      	mov	bp, MonitorSWMes		; BP=当前串的偏移地址
      	mov	ax, ds					; ES:BP = 串地址
      	mov	es, ax		 			; 置ES=DS
      	mov	cx, MesLength  			; CX = 串长
      	mov	ax, 1301h		 		; AH = 13h（功能号）、AL = 01h（光标置于串尾）
      	mov	bx, 0007h		 		; 页号为0(BH = 0) 黑底白字(BL = 07h)
         	mov dh, 6		       		; 行号=0
      	mov	dl, 0			 		; 列号=0
      	int	10h			 			; 打印提示信息
      ```

      ​		接着是打印提示信息的模块。该模块调用了BIOS的`int 10h`，打印字符串，提示可以输入`a`,`b`,`c`,`d`来打开不同的用户程序，或者输入`p`来清空屏幕。

      ```assembly
      	;读入键盘，选择用户程序
      SelectProgram:
      	mov ah, 0				 
      	int 16H				;功能号为0，调用键盘读取
      	mov byte[_input],al
      	call _choosePro
      	
      	mov ah,0
      	mov bx,ax
      	sub bx,1
      	jz clearScreen		;调用清屏功能
      	mov bx,ax
      	sub bx,0
      	jz WrongInput		;输入不合法
      ```

      ​		然后是读入并解析指令，如上方代码块所示。首先调用BIOS的`int 16h`读入键盘存进`ax`寄存器，然后将`al`赋值给变量`_input`（因为在c代码中`input`变量为`char`类型，只需要四位，用`al`寄存器而不用`ax`寄存器）。然后调用c语言模块的`_choosePro`函数对指令进行解析。

      ​		指令为`a`、`b`、`c`、`d`时，该函数分别返回整数3、4、5、6，而指令为`p`时返回1。其他情况都是指令错误，一律返回0。返回值会存在寄存器`ax`中。

      ​		之后将`ax`赋值给`bx`，依次检测是否为1或0。若是，则进入相应模块，否则往下继续执行。

      ```assembly
      ;启动用户程序
      	mov cl,al
      	mov ax,cs                ;段地址,存放数据的内存基地址
      	mov es,ax                ;设置段地址（不能直接mov es,段地址）
      	mov bx, UserProPos		 ;偏移地址; 存放数据的内存偏移地址
      	mov ah,2                 ;功能号2,表示读入
      	mov al,1                 ;扇区数
      	mov dl,0                 ;驱动器号 ; 软盘为0，硬盘和U盘为80H
      	mov dh,0                 ;磁头号 ; 起始编号为0
      	mov ch,0                 ;柱面号 ; 起始编号为0
      	int 13H ;                调用读磁盘BIOS的13h功能
      	; 用户程序已加载到指定内存区域中
      	jmp UserProPos
      ```

      ​		往下继续执行则表示指令为读入用户程序。返回值即为所选的用户程序所在的盘块。所以直接用`mov cl al`对`cl`进行赋值，表示需要读的盘块。然后调用BIOS的`int 13H`将用户程序读入内存9000h处。

      ```assembly
      ;输入错误
      WrongInput:
      	mov	bp, ErrorMes			; BP=当前串的偏移地址
      	mov	ax, ds					; ES:BP = 串地址
      	mov	es, ax		 			; 置ES=DS
      	mov	cx, ErrorLen  			; CX = 串长
      	mov	ax, 1301h		 		; AH = 13h（功能号）、AL = 01h（光标置于串尾）
      	mov	bx, 0007h		 		; 页号为0(BH = 0) 黑底白字(BL = 07h)
         	mov dh, 7		       		; 行号=0
      	mov	dl, 0			 		; 列号=0
      	int	10h			 			; 打印提示信息
      	jmp SelectProgram
      	
      _end:
      	jmp $
      
      MonitorSWMes:
          db 'Enter a, b, c or d to run user program or enter p to clear screen.'
      MesLength  equ ($-MonitorSWMes)
      ErrorMes:
          db 'Invalid input!Please enter a,b,c,d or p.'
      ErrorLen  equ ($-ErrorMes)
      ```

      ​		最后是输入错误的模块，打印错误信息并跳转回到输入指令的模块·`SelectProgram`。提示信息见代码最后部分。

      ​		然后是c语言的部分。

      ```c
      /* MonitorSoftware2.c */
      char input='p';
      
      int choosePro(){
      	if(input=='a')return 3;
      	if(input=='b')return 4;
      	if(input=='c')return 5;
      	if(input=='d')return 6;
      	if(input=='p')return 1;
      	return 0;
      }
      ```

      ​		c代码部分比较简单，`input`变量为输入的指令，被x86汇编代码部分所赋值。在汇编代码调用`choosePro`函数后，依据`input`变量的值返回不同的值。如果是`a`到`d`，表示启动用户程序，则返回用户程序所在的盘块号；如果是`p`，表示清屏，则返回1，其他情况都是错误指令，一律返回0表示错误。

      

      ​		最后，使用指令`nasm -f elf32 MonitorSoftware1.asm -o MonitorSoftware1.o`汇编x86汇编代码,`gcc -march=i386 -m32 -mpreferred-stack-boundary=2 -ffreestanding -c MonitorSoftware2.c -o MonitorSoftware2.o `编译c语言代码，使用指令`ld -m elf_i386 -N MonitorSoftware1.o MonitorSoftware2.o -Ttext 0x100 --oformat binary -o MSW.com`将两个目标文件进行链接即可。生成的二进制文件命名为`MSW.com`。需要注意的是，在链接时，使用参数`-Ttext 0x100`生成`.com`文件，代码偏移量为100h而不是8100h，这是因为在代码块里，有寄存器`cs=800h`，只要使得偏移量为100h，程序就能被读入内存的`cs:100h`，即8100h处。

   5. 编写用户程序。用户程序和实验一、实验二的字符串反弹程序大同小异，在此就不放出全部的代码。以下仅仅给出重要的改动部分的代码。以四个用户程序中以`a.asm`为例。

      ```assembly
      ;a.asm
      ...
      
      dl2dr:
      	mov bx,word[x]
      	mov ax,12
      	sub ax,bx
          jz  back
      
      ...
      
      back:
      	jmp 0x800:0x100
      ```

      ​		需要对程序中的退出模块进行改写。在`word[y]`达到-1时进入`dl2dr`模块后，若`word[x]`为12，则满足退出条件，进入`back`模块。`back`模块使用的是`jmp 0x800:0x100`。这样一来，在跳转时，寄存器会自动设置`cs=800h,ip=100h`。因为在监控程序中，将用户程序读入内存和跳转都是依赖于`cs`寄存器的相对地址，这里必须将`cs`改回800h，之后监控程序才正常地继续调用其他的用户程序。

      ​		所有用户指令通过`nasm a.asm -o a.com`即可。

   6. 使用软件`WinHex`将所有的二进制文件写入软盘文件`MyOS.img`。我对软盘的组织方式为：

      - 首扇区：引导程序，将操作系统内核（监控程序部分）读入内存并转交控制权

      - 第二个扇区：操作系统（监控程序）

      - 第三~六个扇区：依次存放用户程序`a``b``c``d`。

        ![5](PIC\\5.png)

        ![6](PIC\\6.png)

   7. 最后，使用虚拟机`VM ware`读取软盘查看效果。

      开机后的画面如图所示。第一句为首扇区的引导程序打印的提示信息，中间的是监控程序打印的提示信息，表示监控程序已经成功地被读入内存并获得了控制权。

      ![7](PIC\\7.png)
      
      按下`p`可以进行清屏操作，如下图所示。（提示信息是清屏后打印的）
      
      ![8](PIC\\8.png)
      
      ​	按下错误指令，会打印错误提示并允许重新输入。
      
      ​	![9](PIC\\9.png)
      
      ​	按下`b`，可以执行用户程序`b`。
      
      ![10](PIC\\10.png)
      
      ​	因为没有自动清屏功能，b程序的显存修改结果会保留。再输入`c`，如下图所示：
      
      ![11](PIC\\11.png)
      
      输入`p`，能够正常清屏，如下图所示。
      
      ![12](PIC\\12.png)
      
      至此，本实验已经完成。

## 实验总结

​		本次实验的难点主要在两个方面，一个是c语言与x86汇编语言的混编，另一个是如何让引导程序正确地将操作系统内核读入内存并移交控制权，再由监控程序将用户程序读入内存并移交控制权。

​		在进行混编时，依据老师ppt上的指令分别对c源码、x86源码进行编译和汇编，并对生成的目标文件进行链接时出现了一些错误。我使用的工具包是自己安装的，没有用老师提供的。ppt上的理论部分说，`gcc`的编译结果的变量名和x86汇编代码相同，而我在链接时却遇到了`undefined reference to xxx`的错误。经过对c代码的`gcc -S`只进行预处理和汇编处理，查看c源码生成的汇编代码后才得知，我的`gcc`编译结果中，汇编代码的变量名是c中变量名之前加上下划线`_`。

​		经过重新修改程序和编译后，在进行链接时，`ld`工具又会报错：`ld cannot perform PE operations on non PE output file`。经网上查阅资料，通过`stackoverflow`网站上的一篇[帖子](https://stackoverflow.com/questions/30939593/mingws-ld-cannot-perform-pe-operations-on-non-pe-output-file)，我最终得知：`ld`可以对两种文件进行链接：`PE`文件和`ELF`文件。尽管最后将其后缀名定为`.o`，上述`gcc`和`nasm`得到的目标文件都是`ELF`文件，而ppt中指令`ld -m i386pe -N showstrn.o upper.o -Ttext 0x100 --oformat binary -o boot.bin`中，使用的参数`-m i386pe`表明是以`PE`文件的方式进行链接，所以会出错。将参数改为`-m elf_i386`即可对`ELF`文件进行链接。

​		另外，在链接时，还会出现`cannot find entry symbol _start`的警告。这是因为像c语言的`main`函数一样，以x86作为程序入口时，`ld`需要标签`_start`对程序入口进行定位，而且在汇编程序开头还必须要加上`global _start`，将`_start`声明为全局的。在`_start`缺失时，链接时会默认将程序代码段的第一句当做程序入口。

​		最后是引导程序启用监控程序、监控程序启动用户程序。在裸机读软盘时，首扇区的程序会从`7c00h`处开始运行，存在软盘其他盘块的程序都为`.com`二进制文件，需要有`100h`的偏移。在代码实现时，单纯的x86汇编代码在开始处加上`org 100h`，混编代码在链接时加上参数`-Ttext 0x100`即可。然而在程序调用的过程中使用的是相对位置而不是绝对位置，这使得我一开始调试了很久都没能成功地实现程序调用。上述的偏移量都是对当前`cs`寄存区的偏移量而不是绝对地址。所以在引导程序启动监控程序时，要用`jmp 0x800:0x100`修改`cs`的值才能成功地访问到偏移量为100h的监控程序。而监控程序将用户程序写内存时，指定的地址也是相对当前`cs`的偏移地址而不是绝对地址。了解了这一点后，才能实现程序的写内存与控制权移交。

​		