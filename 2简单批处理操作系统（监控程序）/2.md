# 实验项目2
## 个人信息(github version)
- 专业：18级计算机科学二班 
- 学号：18340057
- 姓名：ender507

## 实验题目
加载执行COM格式用户程序的监控程序(开发最原始的操作系统)

## 实验目的
1. 了解监控程序执行用户程序的主要工作
2. 了解一种用户程序的格式与运行要求
3. 加深对监控程序概念的理解
4. 掌握加载用户程序方法
5. 掌握几个BIOS调用和简单的磁盘空间管理

## 实验要求
1. 知道引导扇区程序实现用户程序加载的意义
2. 掌握COM/BIN等一种可执行的用户程序格式与运行要求
3. 将自己实验一的引导扇区程序修改为3-4个不同版本的COM格式程序，每个程序缩小显示区域，在屏幕特定区域显示，用以测试监控程序，在1.44MB软驱映像中存储这些程序。
4. 重写1.44MB软驱引导程序，利用BIOS调用，实现一个能执行COM格式用户程序的监控程序。
5. 设计一种简单命令，实现用命令交互执行在1.44MB软驱映像中存储几个用户程序。
6. 编写实验报告，描述实验工作的过程和必要的细节，如截屏或录屏，以证实实验工作的真实性

## 实验方案
1. 对实验1写过的程序进行改写。实验1的程序为能在整个窗口上进行飞行和反弹的字符，改写后的程序有四个，分别在窗口左上、右上、左下、右下进行飞行和反弹。这四个程序统称为用户程序。
2. 使用x86汇编语言编写一个监控程序，能够读取用户指令，并依据指令选择运行上述的用户程序，并且能够在运行后回到监控程序，运行新的用户程序
  
## 实验过程
1. 对实验1程序的改写。改写后的程序分别为a.asm、b.asm、c.asm和d.asm。四个程序大体相同，下面以a.asm为例。
```x86asm
    Dn_Rt equ 1     ;D-Down,U-Up,R-right,L-Left
    Up_Rt equ 2
    Up_Lt equ 3
    Dn_Lt equ 4
    delay equ 50000 ;计时器延迟计数,用于控制画框的速度
    ddelay equ 580  ;计时器延迟计数,用于控制画框的速度
    org 8100h	    ;程序加载到该位置
	home equ 7c00h
start:
	mov cl,0Fh		;字符初始颜色
    mov ax,cs
	mov es,ax		; ES = 0
	mov ds,ax		; DS = CS
	mov es,ax		; ES = CS
	mov	ax,0B800h	; 文本窗口显存起始地址
	mov	gs,ax		; GS = B800h
    mov byte[char],'A'
loop1:
	dec word[count]		    ;递减计数变量
	jnz loop1				;count大于0时一直循环，以实现延迟显示
	mov word[count],delay	;count为0时重新赋delay给count
	dec word[dcount]		;递减计数变量
    jnz loop1				;dcount控制延迟同上，总延迟为delay*ddelay
	mov word[count],delay
	mov word[dcount],ddelay

    mov al,1
    cmp al,byte[rdul]
	jz  DnRt			;rdul等于1时跳转至DnRt
    mov al,2
    cmp al,byte[rdul]	;rdul等于2时跳转至UpRt
	jz  UpRt
    mov al,3
    cmp al,byte[rdul]	;rdul等于3时跳转至UpLt
	jz  UpLt
    mov al,4
    cmp al,byte[rdul]	;rdul等于4时跳转至DnLt
	jz  DnLt
    jmp $				;不满足上述情况则退出程序
	
;向右下移动
DnRt:			
	inc word[x]
	inc word[y]			;x和y坐标都递增
	mov bx,word[x]
	mov ax,12
	sub ax,bx
    jz  dr2ur			;x等于12时出界，改为向右上
	mov bx,word[y]
	mov ax,40
	sub ax,bx
    jz  dr2dl			;y等于40时出界，改为向左下
	jmp show
dr2ur:
    mov word[x],10
    mov byte[rdul],Up_Rt;之后改为向右上运动	
    jmp show
dr2dl:
    mov word[y],38
    mov byte[rdul],Dn_Lt;之后改为向左下运动	
    jmp show
	
;向右上移动
UpRt:
	dec word[x]
	inc word[y]			;x递减，y递增
	mov bx,word[y]
	mov ax,40
	sub ax,bx
    jz  ur2ul			;y等于40时出界，改为向左上
	mov bx,word[x]
	mov ax,-1
	sub ax,bx			
    jz  ur2dr			;x等于-1时出界，改为向右下
	jmp show
ur2ul:
    mov word[y],38
    mov byte[rdul],Up_Lt;之后改为向左上运动	
    jmp show
ur2dr:
    mov word[x],1
    mov byte[rdul],Dn_Rt;之后改为向右下运动	
    jmp show
	
;向左上移动
UpLt:
	dec word[x]	
	dec word[y]			;x和y坐标都递减
	mov bx,word[x]
	mov ax,-1
	sub ax,bx
    jz  ul2dl			;x等于-1时出界，改为向左下
	mov bx,word[y]
	mov ax,-1
	sub ax,bx
    jz  ul2ur			;y等于-1时出界，改为向右上
	jmp show
ul2dl:
    mov word[x],1
    mov byte[rdul],Dn_Lt;之后改为向左下运动	
    jmp show
ul2ur:
    mov word[y],1
    mov byte[rdul],Up_Rt;之后改为向右上运动	
    jmp show
	
;向左下移动
DnLt:
	inc word[x]
	dec word[y]			;x递增，y递减
	mov bx,word[y]
	mov ax,-1
	sub ax,bx
    jz  dl2dr			;y等于-1时出界，改为向右下
	mov bx,word[x]
	mov ax,12
	sub ax,bx
    jz  dl2ul			;x等于12时出界，改为向左上
	jmp show
dl2dr:
	mov bx,word[x]
	mov ax,12
	sub ax,bx
    jz  home
    mov word[y],1
    mov byte[rdul],Dn_Rt;之后改为向右下运动	
    jmp show
dl2ul:
    mov word[x],10
    mov byte[rdul],Up_Lt;之后改为向左上运动	
    jmp show
	
;打印屏幕
show:	
    xor ax,ax			;计算显存地址
    mov ax,word[x]
	mov bx,80
	mul bx
	add ax,word[y]
	mov bx,2
	mul bx
	mov bp,ax
	mov ah,cl
	mov al,byte[char]	;AL = 显示字符值
	mov word[gs:bp],ax  ;显示字符的ASCII码值
	;打印我的学号，颜色和当前字符相同
	mov bp,0F00h
	mov al,'1'
	mov word[gs:bp],ax
	add bp,2
	mov al,'8'
	mov word[gs:bp],ax
	add bp,2
	mov al,'3'
	mov word[gs:bp],ax
	add bp,2
	mov al,'4'
	mov word[gs:bp],ax
	add bp,2
	mov al,'0'
	mov word[gs:bp],ax
	add bp,2
	mov al,'0'
	mov word[gs:bp],ax
	add bp,2
	mov al,'5'
	mov word[gs:bp],ax
	add bp,2
	mov al,'7'
	mov word[gs:bp],ax
	jmp loop1
end:
    jmp $                   ; 停止画框，无限循环 
datadef:	
    count dw delay
    dcount dw ddelay
    rdul db Dn_Rt         ;一开始默认向右下运动
    x    dw 7
    y    dw 0
    char db 'A'
```
该程序有以下需要注意的地方：
   - 相较于实验1的程序，本程序对边界进行了修改
   - `org 8100h`语句保证程序加载到内存正确的位置，便于执行
   - 进行了`home equ 7c00h`的定义和加入程序出口，进行`jmp home`回到监控程序
   - 为了使得程序不会大于512kb，我删去了实验1实现的字符和颜色的变换功能
   - 在四个程序中，左下角都会显示我的学号
2. 进行监控程序myOS.asm的编写。监控程序的代码如下：
```x86asm
org  7c00h
OffSetOfUserPrg equ 8100h
Start:
	mov	ax, cs	       ; 置其他段寄存器值与CS相同
	mov	ds, ax	       ; 数据段
	;清屏
	mov	ax,0B800h	; 文本窗口显存起始地址
	mov	gs,ax		; GS = B800h
	mov ax,0
	mov bp,ax		; BP = 0
cls:
	mov byte[gs:bp], ' '
	inc bp
	mov byte[gs:bp], 0fh
	inc bp
	mov ax, bp
	xor ax, 7000h
	jnz cls
	
	mov	bp, Message		 ; BP=当前串的偏移地址
	mov	ax, ds		 ; ES:BP = 串地址
	mov	es, ax		 ; 置ES=DS
	mov	cx, MessageLength  ; CX = 串长
	mov	ax, 1301h		 ; AH = 13h（功能号）、AL = 01h（光标置于串尾）
	mov	bx, 0007h		 ; 页号为0(BH = 0) 黑底白字(BL = 07h)
   	mov dh, 0		       ; 行号=0
	mov	dl, 0			 ; 列号=0
	int	10h			 ; BIOS的10h功能：显示一行字符
SelectProgram:
	mov ah, 0				 
	int 16H				;功能号为0，调用键盘读取
	mov ah, 0
	mov bx, ax
	sub bx, 97
	jz opena
	mov bx, ax
	sub bx, 98
	jz openb
	mov bx, ax
	sub bx, 99
	jz openc
	mov bx, ax
	sub bx, 100
	jz opend
	;输入不满足要求，提示输入有误并重新进行输入
	mov	ax, cs	       ; 置其他段寄存器值与CS相同
	mov	ds, ax	       ; 数据段
	mov	bp, Message2
	mov	ax, ds		 ; ES:BP = 串地址
	mov	es, ax		 ; 置ES=DS
	mov	cx, MessageLength2  ; CX = 串长
	mov	ax, 1301h		 ; AH = 13h（功能号）、AL = 01h（光标置于串尾）
	mov	bx, 0007h		 ; 页号为0(BH = 0) 黑底白字(BL = 07h)
    mov dh, 1			; 行号=1
	mov	dl, 0			 ; 列号=0
	int	10h			 ; BIOS的10h功能：显示一行字符
	jmp SelectProgram
	
opena:
	mov cl,2                 ;起始扇区号 ; 起始编号为1
	jmp LoadnEx
openb:	
	mov cl,3
	jmp LoadnEx
openc:
	mov cl,4
	jmp LoadnEx
opend:	
	mov cl,5
	jmp LoadnEx
	
LoadnEx:
;读软盘或硬盘上的若干物理扇区到内存的ES:BX处：
	mov ax,cs                ;段地址 ; 存放数据的内存基地址
	mov es,ax                ;设置段地址（不能直接mov es,段地址）
	mov bx, OffSetOfUserPrg  ;偏移地址; 存放数据的内存偏移地址
	mov ah,2                 ;功能号2,表示读入
	mov al,1                 ;扇区数
	mov dl,0                 ;驱动器号 ; 软盘为0，硬盘和U盘为80H
	mov dh,0                 ;磁头号 ; 起始编号为0
	mov ch,0                 ;柱面号 ; 起始编号为0
	int 13H ;                调用读磁盘BIOS的13h功能
	; 用户程序已加载到指定内存区域中
	jmp OffSetOfUserPrg

	
AfterRun:
    jmp $                      ;无限循环
Message:
    db '18340057OS is running...Please Enter a,b,c or d to run different program.'
MessageLength  equ ($-Message)
Message2:
    db 'Invalid input!Please Enter a,b,c or d'
MessageLength2  equ ($-Message2)
    times 510-($-$$) db 0
    db 0x55,0xaa
```
该程序需要注意的地方有：
- 在cls语句段，通过对全屏输出空格字符以达到清屏的作用，清屏主要是为了清除在用户程序运行后留下来的画面显存。
- 调用用户程序时，我使用了BIOS的读取键盘字符的功能，即代码中的`int 16H`语句。能够读入一个按键并且不回显。
- 我提供了`a` `b` `c` `d`四个选择分别用来调用上述的四个程序。如果输入不在这四个选择之内，程序将会出现错误信息并允许重新输入。
- 该监控程序只会把选择的用户程序读入内存8100h处并运行，而不会将所有用户程序加入内存并执行选择的那一个。
- 四个用户程序预期在软盘内的存储位置是分别在第2、3、4、5个扇区。
  
3. 将程序进行汇编后，将二进制文件写入虚拟软盘文件。
汇编环节我使用的是nasm。将二进制文件写入虚拟软盘用的是WinHex。写盘后前五个扇区的截图如下：
![1](\\PIC\\1.png)

![2](\\PIC\\2.png)

![3](\\PIC\\3.png)

![4](\\PIC\\4.png)

![5](\\PIC\\5.png)

其中，监控程序存在首扇区，用户程序分别在第2、3、4、5个扇区。

4. 在虚拟机上运行。我使用的虚拟机为VMWare。
打开后可以见到以下内容：

![6](\\PIC\\6.png)

输入不是a、b、c或d的任意字符，程序会报错（调用BIOS的`int 16H`方法不会将按键显示在窗口上，也不需要按回车确认，按下按键即可）：

![7](\\PIC\\7.png)

输入`a`，用户程序a程序正确运行：

![8](\\PIC\\8.png)

过了一会儿，a程序会结束，并返回监控程序，如图所示。

![9](\\PIC\\9.png)

我们还可以接着按键，继续调度其他用户程序。比如这次我按了`b`，结果如图所示。

![10](\\PIC\\10.png)

同样，按`c`或者`d`的结果如下图所示。

![11](\\PIC\\11.png)

![12](\\PIC\\12.png)

我的程序还支持长串输入。比如直接输入`bcada`,程序将能够依次按bcada的顺序执行。执行过程和单次运行一个程序没区别，故不提供截图。

至此，本次实验已经完成。
## 实验总结
本次实验的难点在于对细节的把握。对于一个普通的x86汇编程序的汇编结果，可能是com或bin等二进制文件，不同的文件代码的偏移量是不同的。不同的偏移量需要程序员在编写汇编程序时用`org`指令进行自定义。而在本次实验中的用户程序，在内存中的偏移量是不定的。也就是说，将用户程序写入软盘，在操作系统将程序读入内存后，程序的执行是不会看文件格式的，而是会通过内存指针，运行到一处就执行那里的代码。因此，程序的加载位置和内存指针的改变需要依据需求进行。在这次的程序中，在指定了代码段地址的前提下，我让监控程序用户程序加载到内存的8100h处，并在用户程序中也指明了相同的代码偏移量，这样才能正确执行。在此之前，我反复尝试常用的100h或7c00h的偏移量，监控程序都不能正确地跳转到用户程序。

其次，从监控程序和用户程序之间的跳转也是相当重要的。执行用户程序后，用户程序必须知道监控程序的准确的内存地址才能跳转回去，而即便跳转回去，用户程序留下的显存信息也必须删除，这就是我在监控程序里加入了一个清屏模块的原因。这样既能更好的实现监控程序对各个用户程序调度的效果，也能更方便用户使用和查看。