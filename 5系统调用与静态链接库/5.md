# 实验项目5

## 个人信息(github version)

- 专业：18级计算机科学二班 
- 学号：18340057
- 挺难的，我现在只想这门课快点学完

## 实验题目

实现系统调用

## 实验目的

1. 学习掌握PC系统的软中断指令
2. 掌握操作系统内核对用户提供服务的系统调用程序设计方法
3. 掌握C语言的库设计方法
4. 掌握用户程序请求系统服务的方法

## 实验要求

1. 了解PC系统的软中断指令的原理
2. 掌握x86汇编语言软中断的响应处理编程方法
3. 扩展实验四的的内核程序，增加输入输出服务的系统调用。
4. C语言的库设计，实现`putch()`、`getch()`、`printf()`、`scanf()`等基本输入输出库过程。
5. 编写实验报告，描述实验工作的过程和必要的细节，如截屏或录屏，以证实实验工作的真实性

## 实验内容

1. 修改实验4的内核代码，先编写`save()`和`restart()`两个汇编过程，分别用于中断处理的现场保护和现场恢复，内核定义一个保护现场的数据结构，以后，处理程序的开头都调用`save()`保存中断现场，处理完后都用`restart()`恢复中断现场。
2. 内核增加`int 20h`、`int 21h`和`int 22h`软中断的处理程序，其中，`int 20h`用于用户程序结束时返回内核准备接受命令的状态；`int 21h`用于系统调用，并实现3-5个简单系统调用功能；`int 22h`功能未定，先实现为屏幕某处显示`INT22H`。
3. 保留无敌风火轮显示，取消触碰键盘显示`OUCH!`的功能
4. 进行C语言的库设计，实现`putch()`、`getch()`、`gets()`、`puts()`、`printf()`、`scanf()`等基本输入输出库过程，汇编产生`libs.obj`
5. 利用自己设计的C库libs.obj，编写一个使用这些库函数的C语言用户程序，再编译,在与libs.obj一起链接，产生COM程序。增加内核命令执行这个程序
6. 编写实验报告，描述实验工作的过程和必要的细节，如截屏或录屏，以证实实验工作的真实性

## 实验过程

#### save()和restart()的实现		

首先是对`save()`和`restart()`的编写。`save()`过程如下：

```c
int _es, _ds, _di, _si, _bp, _sp, _dx, _cx, _bx, _ax, _ss;
```

```asm
save:
	; 调用中断时会先将FLAGS，CS，IP入栈
	mov	word[_ss], ss
	mov	word[_ax], ax
    mov	word[_bx], bx
	mov	word[_cx], cx
	mov	word[_dx], dx
	mov	word[_sp], sp
	mov	word[_bp], bp
	mov	word[_si], si
	mov	word[_di], di
	mov	word[_ds], ds
	mov	word[_es], es
	ret
```

​		即在c语言部分声明变量，每个变量对应一个寄存器，而在汇编部分，通过`extern`语句引入c变量后将寄存器的值直接赋给变量。值得注意的是，考虑到`save()`模块只在发生软件中断时被调用，而软中断发生时，`flags` `cs` `ip`寄存器会被**自动压栈**，在从中断返回时，即执行命令`iret`时，会相应地**自动出栈**，所以这些寄存器不需要特地去保存。这里我保存了其他的11个寄存器。

​		相应的，`restart`过程如下：

```asm
restart:
	mov ax, cs
	mov ds, ax
	mov es, ax
	mov	ss, word[_ss]
	mov	ax, word[_ax]
    mov	bx, word[_bx]
	mov	cx, word[_cx]
	mov	dx, word[_dx]
	mov	sp, word[_sp]
	mov	bp, word[_bp]
	mov	si, word[_si]
	mov	di, word[_di]
	mov	ds, word[_ds]
	mov	es, word[_es]
	ret
```

​		只需要把相应的变量值赋值回对应的寄存器即可。需要注意，从中断返回时必须给端口`20h`和`0A0H`发送数据`20H`表示中断结束，发送数据时不能直接发送数字而要通过寄存器，因此之后`ax`寄存器的值需要改变为`20H`用于发送EOI，之后再变回来。这里给`ax`赋值只为了形式上的统一，而并没有实际用途。

#### int 20h~22h的实现

##### 中断向量的设置

​		接着是三个软中断处理程序的加入。在内核初始化时，需要将这些中断向量进行改写，如以下代码所示。其中的`wheel`是保留的上次试验实现的“风火轮”时钟中断。

```asm
	mov	bx, es					; 用bx暂存es寄存器的值
	mov	ax, 0
	mov	es, ax
	mov word[es:20h],wheel		; 设置时钟中断向量的偏移地址
	mov	word[es:80h],int20h		; 设置20号中断的中断向量的偏移地址,之后两句同理
	mov	word[es:84h],int21h
	mov	word[es:88h],int22h
	mov ax,cs 
	mov word[es:22h],ax			; 设置时钟中断向量的段地址=CS
	mov	word[es:82h],ax			; 设置20号中断的中断向量的段地址,之后两句同理
	mov	word[es:86h],ax
	mov	word[es:8ah],ax
	mov	es, bx					; 恢复bx寄存器的值
```

​		以`int 20h`为例，计算中断向量的方法如下：20H号中断的值乘以四，即`20H * 4 = 80H`，故20H号中断的中断向量的起始位置为`[0:80H]`，连续两个字节为偏移地址，传入中断处理模块所在的位置，再之后连续两个字节为段地址，传入`cs`寄存器的值。

​		之后是具体每个中断的功能。

##### 20H号中断

​		先讨论`20H`号中断，用于用户程序结束时返回内核准备接受命令的状态，即用户程序返回内核时能够直接调用`int 20H`来返回内核。实现的代码如下：

```asm
int20h:
	call 	save
	jmp		100h:100h
```

​		内核的内存地址对用户程序不透明，用户程序一般通过`ret`语句返回内核而不是直接跳转到内核所在的物理地址。但是在中断程序的编写中，中断程序是由操作系统内核设置的，这时内核所在的物理地址是已知的，可以直接跳转至内核被载入的物理地址处。

##### 22H号中断

​		为了方便讨论，在讨论`21H`号中断前先讨论`22H`号中断。`22H`号中断需要在屏幕上某处显示`INT22H`。

```asm
int22h:
   	call	save
	mov 	ax,cs           ;段地址 ; 存放数据的内存基地址
    mov 	es,ax           ;设置段地址（不能直接mov es,段地址）
	mov		bp, int22hMes	; BP=当前串的偏移地址
	mov		ax, ds			; ES:BP = 串地址
	mov		es, ax			; 置ES=DS
	mov		cx, int22hMesLen; CX = 串长
	mov		ax, 1301h		; AH = 13h（功能号）、AL = 01h（光标置于串尾）
	mov		bh, 00h			; 页号为0(BH = 0)
	mov 	bl, 0Fh
   	mov 	dh, 24			; 行号=24
	mov		dl, 0			; 列号=0
	int		10h				; 打印提示信息
	call	restart 
	jmp		intEnd

int22hMes:
	db 'INT22H'
int22hMesLen equ ($-int22hMes)
```

​		这里我直接调用了BIOS的`10H`中断，在屏幕上显示一串字符串。行号列号分别为24和0，即在屏幕的第25行第1列处显示。

##### 21H号中断

​		21H号中断用于用户程序进行系统调用，需要依据功能号（定为`ah`寄存器）的不同而实现不同的功能。模块主体如下：

```asm
int21h:
	call	save
   	mov		dh, ah
	dec		dh
	jz		getTime 		; ah = 1
	dec		dh
	jz		getDate			; ah = 2
	dec		dh
	jz		itoa			; ah = 3
int21hEnd:
   	call 	restart
	jmp	intEnd
```

​		进入该中断模块后，寄存器的值依次减一，当寄存器的值减到0，0标志寄存器会为1，则可以直接通过`jz`命令跳转至相应的功能模块。需要注意的是，采用`jz`命令跳转，当前指令执行的位置不会被压栈，不能靠`ret`返回，需要给之后执行的代码一个标志，即代码中的`int21hEnd`，便于功能模块执行完之后的跳转。

​		21H号中断的功能如下：

-  功能号为1，屏幕输出当前系统时间
- 功能号为2，屏幕输出当前系统日期
- 功能号为3，将参数`bx`由二进制数字转换为`ascii`码。
- 功能号为其他，直接从中断返回

一下为上述功能的具体实现：

​		输出系统时间：（每句操作的具体的解释见注释）

```asm
getTime:
	mov		ah,2h
	int		1Ah				; 调用BIOS获取时间
	mov		ax, 0B800h		; 文本窗口显存起始地址
	mov		gs, ax			; GS = B800h
	mov 	ah, 0Fh			; 0000：黑底、1111：亮白字
	mov 	al, ch			; ch的高四位存储的为小时的十位上的数
	shr		al, 4			; 右移4位获取该数字
	and		al, 0Fh			; 排除其他位的影响
	add		al, 48			; 将二进制数字转换为字符
	mov 	[gs:((80*23+0)*2)], ax	; 屏幕第 24 行, 第 1 列
	mov 	al, ch			; ch的第四位存储的为小时的个位上的数、
	and		al, 0Fh			; 与0Fh相与，获取该数字
	add		al, 48			; 将二进制数字转换为字符
	mov 	[gs:((80*23+1)*2)], ax	; 屏幕第 24 行, 第 2 列
	mov 	al, ':'			; 使用冒号分隔
	mov 	[gs:((80*23+2)*2)], ax	; 屏幕第 24 行, 第 3 列
	mov 	al, cl			; cl的高四位存储的为分钟的十位上的数
	shr		al, 4
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*23+3)*2)], ax	; 屏幕第 24 行, 第 4 列
	mov		al, cl			; cl的低四位存储的为分钟的个位上的数
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*23+4)*2)], ax	; 屏幕第 24 行, 第 5 列
	mov 	al, ':'
	mov 	[gs:((80*23+5)*2)], ax	; 屏幕第 24 行, 第 6 列
	mov 	al, dh			; dh的高四位存储的为秒的十位上的数
	shr		al, 4
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*23+6)*2)], ax	; 屏幕第 24 行, 第 7 列
	mov 	al, dh			; dh的低四位存储的为秒的个位上的数
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*23+7)*2)], ax	; 屏幕第 24 行, 第 8 列
	jmp		int21hEnd
```

​			系统时间的获取主要通过调用BIOS的`int 1AH`，功能号为`2H`。调用后，寄存器`ch`保存当前小时，`cl`保存当前分钟，`dh`保存当前秒。需要注意的是，这里是以BCD码的格式存储的，也就是以4位二进制数表示一位十进制数。以保存小时的`ch`为例，`ch`共8位，高四位的二进制数字表示小时的十进制数字的十位，第四位的二进制数字表示小时的十进制数组的个位。因此，要获取这些字符，必须要先取得这些位。取得十位时先右移四位，将高四位移到低四位，再与0Fh相与，排除其他位的影响，然后加上字符'0'的ascii码值得到数组对应的字符。个位本身就在低四位，与0Fh相与后加上字符'0'即可。分钟和秒同理。

​		然后是获取系统日期：

```asm
getDate:
	mov		ah,4h
	int		1Ah
	mov		ax, 0B800h		; 文本窗口显存起始地址
	mov		gs, ax			; GS = B800h
	mov 	ah, 0Fh			; 0000：黑底、1111：亮白字
	mov 	al, ch
	shr		al, 4
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*22+0)*2)], ax	; 屏幕第 23 行, 第 1 列
	mov 	al, ch					; ch存储年份的前两个数字
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*22+1)*2)], ax	; 屏幕第 23 行, 第 2 列
	mov 	al, cl					; cl存储年份的后两个数字
	shr		al, 4
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*22+2)*2)], ax	; 屏幕第 23 行, 第 3 列
	mov		al, cl
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*22+3)*2)], ax	; 屏幕第 23 行, 第 4 列
	mov 	al, '-'					; 使用'-'分隔
	mov 	[gs:((80*22+4)*2)], ax	; 屏幕第 23 行, 第 5 列
	mov 	al, dh					; dh存储月份
	shr		al, 4
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*22+5)*2)], ax	; 屏幕第 23 行, 第 6 列
	mov 	al, dh
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*22+6)*2)], ax	; 屏幕第 23 行, 第 7 列
	mov 	al, '-'
	mov 	[gs:((80*22+7)*2)], ax	; 屏幕第 23 行, 第 8 列
	mov 	al, dl					; dl存储日子
	shr		al, 4
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*22+8)*2)], ax	; 屏幕第 23 行, 第 9 列
	mov 	al, dl
	and		al, 0Fh
	add		al, 48
	mov 	[gs:((80*22+9)*2)], ax	; 屏幕第 23 行, 第 10 列
	jmp		int21hEnd
```

​		这里依旧是调用BIOS的`int 1AH`，功能号为`4H`。调用后`ch`、`cl`分别存储年份的前两个数组和后两个数字，而`dh`存储月份，`dl`存储日。这里依旧是采用BCD码的存储方式，转换方法和上述的时间转换方法相同。

​		最后是将参数寄存器值对应的数字转换为`ascii`码表示，如数字`5`转换为字符`'5'`。

```asm
itoa:
	add		bx, '0'
	mov		word[_bx], bx
	jmp		int21hEnd
```

​		因为只有一位，只需要加上字符`'0'`对应的ascii码值即可。这里寄存器`bx`不仅作为传递的参数，还要作为返回值，所以将得到的结果存入变量`_bx`，在调用`restart()`过程时，结果就能直接写入`bx`寄存器。

##### 中断返回

```asm
intEnd:
	mov 	al,20h		; AL = EOI
	out 	20h,al		; 发送EOI到主8529A
	out 	0A0h,al		; 发送EOI到从8529A
	mov		ax,word[_ax]
	iret				; 从中断返回
```

​		在调用中断后从中断返回，需要向`20h`和`0A0h`两个端口发送数据`20H`，即EOI，表示中断已经结束。然而传值只能通过寄存器，这里我使用了`ax`寄存器。为了返回后能够恢复`ax`的值，在发送完EOI后应使用`mov ax, word[_ax]`恢复`ax`。

#### 实现的中断处理的测试(test1)

​		注意！为了区别系统调用和程序库的实现，我将测试程序分成了两个，一个用于测试系统调用(`test1`)，一个用于测试c库的实现(`test2`)。下面展示使用了上述实现的中断处理的测试程序。

```asm
;	test1.asm

org 1000h
_start:
	int		22h						; 调用22h号中断
	mov		ah, 1
	int 	21h						; 调用21h号中断，功能号为1
	mov		ah, 2
	int		21h						; 调用21h号中断，功能号为2
	mov		ah, 3
	mov		bx, 1
	int		21h						; 调用21h号中断，功能号为3，参数为1
	mov		ax, 0B800h
	mov		gs, ax
	mov		bh, 0Fh
	mov		[gs:((80*21)*2)], bx	; 将21h号中断的返回结果打印在22行第0列
	int		20h						; 调用20h号中断，返回监控程序
```

​		首先调用22h号中断，在屏幕的25行打印`INT22H`字符，然后调用21h号中断，功能号为1，在屏幕的第24行打印时间，格式为小时：分钟：秒，再调用21h号中断，功能号为2，在屏幕的23行打印日期，格式为年份-月份-日，接着调用21h号中断，功能号为3，参数`bx`设置为1，通过直接写显存的方法在屏幕的22行第0列打印返回结果，预期值为打印字符`'1'`。最后调用20h号中断，直接返回用户程序。

​		通过`gcc -c -m16 -march=i386 -masm=intel -nostdlib -ffreestanding -mpreferred-stack-boundary=2 -lgcc -fno-pie -shared ckernel.c -o ckernel.o`对内核的c部分进行编译，`nasm -f elf32 kernel.asm -o kernel.o`对汇编部分进行汇编，再由`ld -m elf_i386 -N kernel.o ckernel.o -Ttext 0x100  --oformat binary -o kernel.com`进行链接。用`nasm test1.asm -o test1.com`编译第一个测试程序。

​		通过对内核的拓展，内核代码量和最后的二进制可执行文件又有所增加。相对之前的实验，我又改变了软盘内的程序的存储位置。现在用户程序a到d存放在第7到第10个扇区，而`test1`存在**第11个扇区**。同时对`boot`程序，及首扇区的程序进行了改写，在向内存读入内核时一次读入5个盘块大小。

​		使用虚拟机VMWare查看运行情况。使用指令`run test1`执行`test1`程序。注意我对右下角的风火轮进行了保留。

![1](PIC\\1.png)

执行结果如下：

![2](PIC\\2.png)

​		可以看到，屏幕左下方打印了一系列信息。首先字符`'1'`的打印说明了21H中断的3号功能成功实现了，之后日期和时间的正常显示说明了21H中断的2号和1号功能成功实现了。最后的`INT22H`字符说明22H中断成功实现了。

​		继续输入`run d`。

![3](PIC\\3.png)

​		程序d能够正常运行，说明之前在`test1`程序结束后成功回到了操作系统内核，20H号中断也成功实现了。

#### c库的实现

​		c库主要为输入输出提供库过程，因此必须要使用汇编语言。考虑到模块之间的包含关系和逻辑关系，我在这里使用了c和汇编混编的方式。c代码部分作为头文件`libs.h`，被用户程序通过`#include`语句包含，而x86汇编部分`libs.asm`作为静态链接库，在程序的链接过程中与用户程序进行合并。

##### getchar()的实现

```asm
getchar:
	mov		ah, 0	
	int 	16h 		;0号功能调用从键盘读入一个字符放入al中
	mov 	byte[_ch], al
	;打印回车即换行
	mov		bl, 13		
	sub		al, bl
	jz		endl
	; 将输入的字符回显到屏幕上
	mov		ax, 0B800h	; 文本窗口显存起始地址
	mov		gs, ax		; GS = B800h
	mov 	ax, word[row]
	mov		bx, 80
	mul		bx
	add		ax, word[col]
	inc		word[col]
	mov 	bx, 2
	mul 	bx
	mov 	bp, ax
	mov 	ah, 0Fh
	mov 	al, byte[_ch]
	mov 	word[gs:bp], ax
	ret
	
	col dw 0 
	row dw 1
```

​		为了实现回显的字符输入，需要有两个变量`col`和`row`记录当前打印位置的列和行。这里，我在c中定义了一个变量`_ch`，使用BIOS的`int 16H`调用，从键盘读取一个字符，存入变量`_ch`中。然后利用`row`和`col`计算出当前的显存位置，将该字符打印到屏幕上。如果输入是回车则直接换行。换行时从键盘读到的字符为13号，即回车，此时跳转至以下的`endl`模块：

```asm
endl:
	mov word[col], 0
	inc word[row]
	ret
```

​		将当前位置的列改为0，表示列首，同时行号加一，换到下一行。

##### putchar()的实现

```asm
putchar:
	mov		al, 10
	mov		bl, byte[_ch]
	; 输出换行符则跳转至endl
	sub		al, bl
	jz		endl
	mov		ax, 0B800h	; 文本窗口显存起始地址
	mov		gs, ax		; GS = B800h
	mov 	ax, word[row]
	mov		bx, 80
	mul		bx
	add		ax, word[col]
	inc		word[col]
	mov 	bx, 2
	mul 	bx
	mov 	bp, ax
	mov 	ah, 0Fh
	mov 	al, byte[_ch]
	mov 	word[gs:bp], ax
	ret
```

​		`putchar()`也要考虑到换行。而输出字符一般不会使用回车符`'\r'`进行换行而是使用换行符`'\n'`，ascii码值为10。如果输出的字符为换行符则跳转至`endl`模块进行换行。否则通过直接写显存的方法，用变量`row`和`col`获取显存位置，打印当前字符`_ch`。

##### gets()的实现

​		使用gets()读入一行字符串，本质相当于多次读取单个字符，故可以多次调用上面实现的`getchar()`过程实现。这里我采用c实现。

```c
char str[20] = "abcdefghijklmnopqrst"; 

void gets(){
	pos = 0;								//记录写数组的位置
	while(1){
		getchar();		
		if(_ch=='\r'||_ch=='\n')break;		//如果读到换行则结束输入
		str[pos++] = _ch;
	}
	str[pos] = '\0';						//在字符串尾部添加字符'\0'作为结束标志
}
```

​		我定义了一个字符串`str`来存储输入的字符串。在循环中通过不断调用`getchar()`过程将读到的字符写入`_ch`，再把字符`_ch`依次写入字符串。如果读到回车或换行符，则直接跳出循环。此时，回车或换行字符不需要写入字符串。注意，要在读入的字符串末尾再加入一个`'\0'`字符作为字符串的结束标志，便于之后的字符输出。

##### puts()的实现

​		使用`puts()`输出一整行字符串，本质相当于多次输出单个字符，也可以通过多次调动上面已经实现的`putchar()`实现。

```c
void puts(){
	pos = 0;
	while(1){
		_ch = str[pos++];
		if(_ch=='\0')break;		
		putchar();		
	}
	_ch = '\n';
	putchar();
}
```

​		和`gets()`的实现基本相同，通过变量`pos`遍历字符串，将读到的字符存入变量`_ch`中，在循环中不断调用`putchar()`打印当前字符。当`_ch`的值为`'\0'`，说明已经读到了字符串末尾，则直接跳出循环。为了模拟c的`puts()`，我又在字符串末尾输出了一个换行符。

##### getNum()的实现

​		输出和输入实现的目的要实现类似于c的`printf`和`scanf`的功能。考虑到输入和输出一般有两种格式，一种是以字符形式作为输入或输出，一种是以数字形式作为输入或输出。上述模块基本已经实现了`%c`和`%s`，这里我考虑实现两个模块`getNum()`和`putNum()`分别对数字进行输入和输出，实现`%d`的功能。`getNum()`模块如下：

```c
int num = 0;

void getNum(){
	num = 0;
	while(1){
		getchar();		
		if(_ch=='\r'||_ch=='\n')break;
		num *= 10;
		num = num + _ch -'0';
	}
}
```

​		我定义了一个全局变量`num`来存储数字的值。输入数字可以看做输入字符后进行转换，故也可以通过调用`getchar()`实现。再循环中，每次读到一个字符，将会插入当前`num`的最低位，故`num`需要先乘以10，然后加上当前字符对应的数字，即`_ch - '0'`。当输入为回车或者换行符，表示输入结束，则直接跳出循环。

##### putNum()的实现

```c
void putNum(){
	n = 1;
	while(num/n)n*=10;
	n/=10;
	while(n){
		_ch = num/n + '0';
		putchar();
		num %= n;
		n /= 10;
	}
}
```

​		`putNum()`实现的难点在于需要输出整串数字，需要从高位输出到低位。我们能够很方便地获取低位却不能很多方便的获取高位。我首先定义了一个变量`n`，通过不断乘以10，判断`num/n`的大小。若为0，则`n`比`num`多一位。`n/=10`后就和`num`位数相同。在之后的循环中，`num/n`能够取出`num`当前的最高位的数字，加上字符`'0'`进行输出即可。然后`num%=n`可以删去`num`的最高位，`n/=10`使得`n`与`num`的位数一致。当`n`变为0说明`num`的所有位都已经输出，则直接跳出循环。

#### 实现的静态链接库的测试(test2)

​		为了测试上述的库过程，我编写了`test2.c`。

```c
#include"libs.h"

void main(){
	getchar();
    char c = _ch;
	_ch = '\n';
	putchar();
	
    _ch = c;
	putchar();
	_ch = '\n';
	putchar();
	
    gets();
	
    char *string = str;
	for(int i=0; string[i-1]!='\0';i++)str[i] = string[i];
	puts();
	
    getNum();
	int n = num;
	
    num = n;
	putNum();
	back();
}
```

​		通过`#include"libs.h"`，可以直接把库的c部分加入源代码。在编译器预处理前，`libs.h`的全部内容会被加入`test2.c`的开头部分。在`test2.c`中，首先使用了`getchar()`读入字符。结果可以通过使用`_ch`赋值。之后我又将变量`c`的值赋值回`_ch`进行`putchar`打印，证明读取的字符是正确的。之后的字符串输入和数字输入同理，将读入的字符串和数字重新打印，证明读入内容正确。

​		编译的过程中，使用`gcc -c -m16 -march=i386 -masm=intel -nostdlib -ffreestanding -mpreferred-stack-boundary=2 -lgcc -fno-pie -shared test2.c -o test2.o`对`test2.c`进行编译即可。因为`libs.h`通过头文件`#include`的形式加入了`test2.c`，故不需要单独编译。`libs.asm`通过`nasm -f elf32 libs.asm -o libs.o`汇编，之后通过`ld -m elf_i386 -N libs.o test2.o -Ttext 0x1000  --oformat binary -o test2`进行链接即可，与x86汇编和c语言的混编过程基本相同。

​		这里需要注意的是，在链接过程中，汇编模块的`_start`过程是默认的程序入口，而不是c语言的`main`函数，需要在汇编代码中加入以下部分:

```asm
_start:
	mov	ax,cs
	mov	es,ax		; ES = CS
	mov	ds,ax		; DS = CS
	mov	ss,ax		; SS = CS
	mov	sp, 0FFFFh 
	call 	main
	jmp	_end
```

​		`_start`过程通过直接调用c语言的`main`函数以此模拟直接将`main`函数作为程序入口的情形。

​		将test2装入软盘的第12个扇区后使用虚拟机运行。这里test2所占扇区数超过了一个，在装入内存的时候需要注意。

​		输入`run test2`运行程序：

![4](PIC\\4.png)

​		运行时依次输入单个字符、字符串、数字，运行结果如下：

![5](PIC\\5.png)

​		可以看出，字符、字符串、数字都能正常地输入和输出。静态链接库成功实现了。

​		至此，本次实验已经全部完成。

## 实验总结

​		这次实验的难度体现在实现方法的多样性上。相同的过程有不同的实现方式，而不同的方式都有着自己的问题需要解决。找到一条便利且有效的路子相当重要。

​		在`start()`和`restart()`实现的过程中，可以通过c语言或x86汇编实现，而老师给的参考例程都是x86汇编实现的过程。考虑到之后的多道程序操作系统的实现需要建立进程表和不同进程各自的进程状态，使用c的话更加的方便将数据进行统一管理和打包。虽然我在程序中各个寄存器存储变量是独立的，在之后建立多进程模型后可以直接加入c的结构体进行统一管理，还可以加入进程号、进程状态等变量，通过c数组来多个进程的进程状态进行保存和恢复。考虑到代码重用和简便性，我使用了c语言实现。

​		在静态链接库实现时也有两种思路。考虑到库过程需要输入输出，少不了x86汇编部分，而c语言的引入更方便逻辑操作，所以我的库过程是c与x86的混编而不是纯汇编。库过程的c部分可以作为头文件，直接使用`#include`加入用户程序，也可以独立成一个c程序，单独编译后通过链接和用户程序与库过程汇编部分进行链接。我一开始也是这么做的。在链接过程中，需要以`ld libs.o clibs.o test2.o `或者`ld libs.o test2.o clibs.o  `的顺序进行链接（为了简便我省去了链接指令的其他参数），即将程序入口的模块放在首个位置即可。只是每次单独编译链接库过程的c模块比较麻烦，我最后采用了头文件的形式。二者区别不大，很容易相互转换。

​		最后，在进行链接时可以通过`-e main`参数改变程序默认入口而不是使用汇编模块的`_start`作为入口调用c的`main`函数。如果这样做，在进入用户程序时各个寄存器的初始化都交由编译器进行，对栈寄存器等的设置对操作系统开发者不透明，不了解这些寄存器的值进行操作可能会出现未知错误，所以我还是以`_start`作为程序入口，自己初始化寄存器，便于程序管理和监控。

​		另外，在实验要求的部分外，我又对原来的操作系统内核做了如下的改进：

1. 在输入指令时通过BIOS`int 10h`的2号功能实现了光标的输入追踪，即每次输入指令后光标都指向下一个输入位置。

2. 新增变量`proSize`表示程序大小，确定运行的程序后进行赋值，决定程序读入内存的盘块数。主要是为了区分本次实验的`test2.c`（占2个盘块）和其他的程序（只占一个盘块）。

   二者的实现分别靠BIOS调用和将常量改为变量，操作较为简单，不在实验报告中详细介绍，具体实现请见代码部分。

​		